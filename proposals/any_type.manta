// This file demonstrates how any works.
// any is treated like an enum where every type is variant that wraps a value of itself.
// e.g. enum { i8(i8); i16(i16) ... }
// larger types like structs wrap a pointer type rather than the type directly
// e.g. enum { ... str(*str); Vec3(*Vec3) }
//
// technically 'any' is just an empty trait so this is how all traits work
// while the semantics of working with traits like 'any' feel like interacting
// with a sum type (enum), this does not necessarily reflect how they are
// implemented under the hood

mod main

use (
    "fmt"
    "numbers"
)

type Vec3 struct {
    x f64
    y f64
    z f64
}

impl Vec3 {
    fn add(self, other Vec3) Vec3 {
        return Vec3{
            x: self.x + other.x,
            y: self.y + other.y,
            z: self.z + other.z,
        }
    }
}

type Vec2 struct {
    x f64
    y f64
}

impl Vec2 {
    fn add(a, b Vec2) Vec2 {
        return Vec2{
            x: a.x + b.x
            y: a.y + b.y
        }
    }
}

type Add enum {
    .Ok(any)
    .CanNotAddA(any)
    .CanNotAddB(any)
}

fn add(a, b any) Add {
    // check for none here instead of .Ok so we can
    // early return uf numbers::add_any works
    let .None = numbers::add_any(a, b) wrap .Ok

    // match on a to see if it's an addable type
    // even though it's not a number
    match a {
        .Vec2(a) {
            // if a is a Vec2 insist that b is a Vec2
            let .Vec2(b) = b wrap .CanNotAddB
            let v = Vec2::add(a, b)
            return .Ok(v)
        }
        .Vec3(a) {
            // if a is a Vec3 insist that b is a Vec3
            let .Vec3(b) = b wrap .CanNotAddB
            let v = a.add(b)
            return .Ok(v)
        }
        // type matches on a trait must always contain a 'default'
        // arm like this because there is no way to guarentee a trait
        // will not be implemented by some other type in the future
        e { return .CanNotAddA(e) }
    }
}

fn main() {
    let got = add(i32(20), u8(10))
    fmt::printfln("got {}", got)
}

